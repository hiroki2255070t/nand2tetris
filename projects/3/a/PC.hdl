// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // else:             out(t+1) = out(t)
    // else if inc(t):   out(t+1) = out(t) + 1
    Inc16(in=w0, out=wInc);
    Mux16(a=w0, b=wInc, sel=inc, out=w2);

    // else if load(t):  out(t+1) = in(t)
    Mux16(a=w2, b=in, sel=load, out=w3);

    // if      reset(t): out(t+1) = 0
    Mux16(a=w3, b=false, sel=reset, out=w4);
    Register(in=w4, load=true, out=w0, out=out);
}